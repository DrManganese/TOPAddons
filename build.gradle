buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }

    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.1.0"
    id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.64"
}


apply plugin: "net.minecraftforge.gradle.forge"


repositories {
    mavenCentral()
    maven {
        name = "The One Probe"
        url = "https://maven.tterrag.com"
    }
    maven {
        name = "CoFH"
        url = "http://maven.covers1624.net/"
    }
    maven {
        name = "ChickenBones"
        url = "http://chickenbones.net/maven"
    }
    maven {
        name = "Forestry, ICÂ², Binnie's"
        url = "http://maven.ic2.player.to"
    }
    maven {
        name = "Tinkers', JEI"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "Blood Magic"
        url = "http://tehnut.info/maven/"
    }
    maven {
        name = "OpenComputers"
        url = "http://maven.cil.li/"
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
}

configurations.all {
    exclude group: "mezz.jei", module: "jei_1.12.2"
}

dependencies {
    deobfCompile("mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.22-13") { transitive = false }

    deobfCompile "net.industrial-craft:industrialcraft-2:2.8.75-ex112"
    deobfCompile "li.cil.oc:OpenComputers:MC1.12.1-1.7.+"
    deobfCompile "moo-fluids:MooFluids:1.12.2:1.7.11.02a"
    deobfCompile "roost:roost:1.12:1.3.0"
    deobfCompile "lycanites-mobs:lycanitesmobs:1.12.2:2.0.1.2"
    deobfCompile "rustic:rustic:1.1.1"
    deobfCompile "architecturecraft-tridev:architecturecraft:1.12:3.98"
    deobfCompile "unlimited-chisel-works:UnlimitedChiselWorks:0.3.0"

    deobfCompile "net.sengir.forestry:forestry_1.12.2:5.8.0.277+"
    compileOnly("binnie:binnie-mods-1.12.2:2.5.0.164") { transitive = false }

    deobfCompile "slimeknights.mantle:Mantle:1.12-1.3.2.26"
    deobfCompile("slimeknights:TConstruct:1.12.2-2.10.1.95") { transitive = false }

    runtime "reborncore:RebornCore-1.12.2:3.16.4.478:universal"
    deobfCompile "steves-carts-reborn:StevesCarts:1.12.2:2.4.18.92"

    runtime "info.amerifrance.guideapi:Guide-API:1.12-2.1.+"
    deobfCompile "com.wayoftime.bloodmagic:BloodMagic:1.12.2-2.2.+"

    runtime "cucumber:Cucumber:1.12.2:1.1.3"
    deobfCompile "mystical-agriculture:MysticalAgriculture:1.12.2:1.7.3"

    runtime "team.chisel.ctm:CTM:MC1.12.2-0.3.3.22"
    deobfCompile "chisel:Chisel:MC1.12.2:1.0.1.44"

    compile "chameleon:Chameleon:1.12:4.1.3"
    deobfCompile "storage-drawers:StorageDrawers:1.12.2:5.4.0"

    compile "bdlib:bdlib-1.14.3.12:mc1.12.2:dev"
    compile "advanced-generators:generators-0.9.20.12:mc1.12.2:dev"

    runtime "codechicken:CodeChickenLib:1.12.2-3.2+:universal"
    runtime "cofh:CoFHWorld:1.12.2-1.2+:universal"
    runtime "cofh:ThermalFoundation:1.12.2-2.6.3.27:universal"
    compile "cofh:ThermalExpansion:1.12.2-5.5.4.43:deobf"
    compile "cofh:ThermalDynamics:1.12.2-2.5.5.21:deobf"
    compile "cofh:CoFHCore:1.12.2-4.6.3.27:deobf"
    compile "cofh:RedstoneFlux:1.12-2.1.0.+:universal"

    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = mc_version + '-' + MAJOR + '.' + MINOR + '.' + PATCH
group = "io.github.drmanganese"
archivesBaseName = "topaddons"

minecraft {
    version = mc_version + '-' + forge_version
    runDir = "run"
    mappings = mcp_mappings

    replace '@VERSION@', project.version
    useDepAts = true
}


processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    from "LICENSE"
}

task generateGitChangelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    file = new File("CHANGELOG.md")
    fromRef = "v1.11.2-0.15.0"
    toRef = "HEAD"
    templateContent = file('changelog.mustache').getText('UTF-8')
    ignoreCommitsIfMessageMatches = "v.*-.*|Merge"
}

curseforge {
    apiKey = curseforgeToken
    project {
        id = "247111"
        changelogType = "markdown"
        changelog = file("CHANGELOG.md")
        releaseType = "release"
        mainArtifact jar
        addArtifact sourceJar
        relations {
            requiredLibrary "the-one-probe"
        }
    }
}

sourceSets {
    main {
        java {
            exclude '**/addons/AddonNeoTech.java'
        }
    }
}

idea { module { inheritOutputDirs = true } }
