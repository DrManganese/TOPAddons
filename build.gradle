buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "4.1.+", changing: true
        classpath group: "gradle.plugin.com.matthewprenger", name : "CurseGradle", version: "1.4.0"
    }
}
apply plugin: "net.minecraftforge.gradle"
apply plugin: "com.matthewprenger.cursegradle"

version = "$mc_version-$MAJOR.$MINOR.$PATCH"
group = "io.github.drmanganese.topaddons"
archivesBaseName = "topaddons"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = "1.8"

minecraft {
    mappings channel: mcp_channel, version: mcp_mappings
    accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")

    runs {
        client {
            workingDirectory project.file("run")
            property "forge.logging.console.level", "debug"
            mods {
                topaddons {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run-server")
            property "forge.logging.console.level", "debug"
            mods {
                topaddons {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        name = "tterag" // theoneprobe
        url = "https://maven.tterrag.com"
    }
    maven {
        name = "jaquadro" // storagedrawers
        url = "https://dl.bintray.com/jaquadro/dev/"
    }
    maven {
        name = "Progwml6" // jei
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "cursemaven" // thermal
        url = "https://www.cursemaven.com"
    }
    maven {
        name = "blamejared" // patchouli
        url = "https://maven.blamejared.com"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:$mc_version-$forge_version"
    compile fg.deobf("mcjty.theoneprobe:TheOneProbe-1.16:1.16-3.0.7-13") {transitive = false}

    // Addon dependencies. Set to compileOnly for quicker test client launching
    compile fg.deobf("com.jaquadro.minecraft.storagedrawers:StorageDrawers:1.16.3-8.2.2")

    compile fg.deobf("curse.maven:industrial-foregoing-266515:3223241")
    compile fg.deobf("curse.maven:titanium-287342:3223141")

    compile fg.deobf("curse.maven:cofh-core-69162:3155824")
    compile fg.deobf("curse.maven:thermal-foundation-222880:3155830")
    compile fg.deobf("curse.maven:thermal-expansion-69163:3155832")

    compile fg.deobf("curse.maven:blood-magic-224791:3184485")

    runtimeOnly fg.deobf("mezz.jei:jei-1.16.4:+")
    //runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:1.16.4-50")
}

jar {
    manifest {
        attributes([
            "FMLAT": "accesstransformer.cfg",
            "Specification-Title": "topaddons",
            "Specification-Vendor": "DrManganese",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": getArchiveVersion(),
            "Implementation-Vendor" :"DrManganese",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

curseforge {
    apiKey = curseforgeToken
    project {
        id = "247111"
        changelogType = "markdown"
        changelog = file("CHANGELOG.md")
        releaseType = "beta"
        mainArtifact(jar) {
            relations {
                requiredDependency "the-one-probe"
            }
        }
    }
}
